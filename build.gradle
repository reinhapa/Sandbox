plugins {
  id 'java-library'
  id 'war'
  id 'eclipse-wtp'
  id 'com.google.osdetector' version '1.6.2'
  id 'com.github.ben-manes.versions' version '0.28.0'
//  id 'net.minecrell.licenser' version '0.4.1'
  id 'net.nemerosa.versioning' version '2.12.1'
  id 'org.owasp.dependencycheck' version '5.3.2.1'
}

group = 'net.reini'
version = '0.0.1-SNAPSHOT'
description = "Sandbox project for pure testing purposes"
ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    implementation 'com.airhacks:afterburner.fx:1.+'
    implementation 'com.github.sarxos:webcam-capture:0.+'
    implementation 'com.google.zxing:core:3.+'
    implementation 'com.google.zxing:javase:3.+'
    implementation 'javax.cache:cache-api:1.+'
    implementation 'org.ehcache:ehcache:3.+'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.+'
    implementation 'org.glassfish.jersey.core:jersey-client:2+'
    implementation 'org.bouncycastle:bcprov-jdk14:1.+'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.2'

    implementation 'jakarta.activation:jakarta.activation-api:1.2.2'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.0.1'

    implementation 'org.jboss.weld.se:weld-se-core:3.1.4.Final'
    implementation 'org.jboss:jandex:2.1.3.Final'

    // javaFX
    implementation "org.openjfx:javafx-base:11.+:$platform"
    implementation "org.openjfx:javafx-graphics:11.+:$platform"
    implementation "org.openjfx:javafx-fxml:11.+:$platform"
    implementation "org.openjfx:javafx-controls:11.+:$platform"
    
    compileOnly 'jakarta.platform:jakarta.jakartaee-api:8.0.0'

    // websocket
    compileOnly 'jakarta.websocket:jakarta.websocket-client-api:1.1.2'

    runtimeOnly 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.16'
    runtimeOnly 'net.reini:slf4j-cdi:1.0'

    testImplementation 'easymock:easymock:2.0'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
    testImplementation 'org.jmockit:jmockit:1.49'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-migrationsupport:5.6.1'

    testImplementation 'jakarta.websocket:jakarta.websocket-client-api:1.1.2'

    testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.6.1'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.6.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'

    testRuntimeOnly 'jakarta.platform:jakarta.jakartaee-api:8.0.0'
    testRuntimeOnly 'net.reini:slf4j-cdi:1.1'
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '-Xlint:all',
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls,javafx.fxml'
        ]
    }
}

test {
    doFirst {
        copy {
            from 'src/main/resources/META-INF/beans.xml'
            into 'build/classes/java/main/META-INF'
        }
    }
    useJUnitPlatform()
    testLogging {
        events 'skipped'
    }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'cr', 'ea', 'm', 'pr', 'preview', 'rc'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
