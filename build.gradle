plugins {
  id 'java-library'
  id 'war'
  id 'eclipse-wtp'
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'com.google.osdetector' version '1.6.2'
  id 'net.minecrell.licenser' version '0.4.1'
  id 'net.nemerosa.versioning' version '2.14.0'
  id 'org.owasp.dependencycheck' version '5.3.2.1'
}

group = 'net.reini'
version = '0.0.1-SNAPSHOT'
description = "Sandbox project for pure testing purposes"
ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

sourceCompatibility = '14'
targetCompatibility = '14'

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    implementation 'com.airhacks:afterburner.fx:1.7.0'
    implementation 'com.github.sarxos:webcam-capture:0.3.12'
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'com.google.zxing:javase:3.4.0'
    implementation 'javax.cache:cache-api:1.1.1'
    implementation 'org.bouncycastle:bcprov-jdk14:1.65'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.8.0.202006091008-r'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.4'
    implementation 'org.ehcache:ehcache:3.8.1'
    implementation 'org.glassfish.jersey.core:jersey-client:2.31'

    implementation 'jakarta.activation:jakarta.activation-api:1.2.2'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.3'

    implementation 'org.jboss.weld.se:weld-se-core:3.1.4.Final'
    implementation 'org.jboss:jandex:2.1.3.Final'

    // javaFX
    implementation "org.openjfx:javafx-base:14.0.1:$platform"
    implementation "org.openjfx:javafx-graphics:14.0.1:$platform"
    implementation "org.openjfx:javafx-fxml:14.0.1:$platform"
    implementation "org.openjfx:javafx-controls:14.0.1:$platform"
    
    compileOnly 'jakarta.platform:jakarta.jakartaee-api:8.0.0'

    // websocket
    compileOnly 'jakarta.websocket:jakarta.websocket-client-api:1.1.2'

    runtimeOnly 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.17'
    runtimeOnly 'net.reini:slf4j-cdi:1.1'
    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'easymock:easymock:2.0'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
    testImplementation 'org.jmockit:jmockit:1.49'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-migrationsupport:5.6.2'

    testImplementation 'jakarta.websocket:jakarta.websocket-client-api:1.1.2'

    testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.6.2'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    testRuntimeOnly 'jakarta.platform:jakarta.jakartaee-api:8.0.0'
    testRuntimeOnly 'net.reini:slf4j-cdi:1.1'
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '-Xlint:all',
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls,javafx.fxml'
        ]
    }
}

test {
    doFirst {
        copy {
            from 'src/main/resources/META-INF/beans.xml'
            into 'build/classes/java/main/META-INF'
        }
    }
    useJUnitPlatform()
    testLogging {
        events 'skipped'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Sandbox for various test',
                   'Implementation-Version': version,
                   'Build-Revision': versioning.info.commit,
                   'Automatic-Module-Name': "net.reini.sandbox"
        metaInf {
            from file('.')
            include 'LICENSE'
        }
    }
}
dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'pr', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
