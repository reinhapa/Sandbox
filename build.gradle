plugins {
  id 'java-library'
  id 'war'
  id 'eclipse-wtp'
  id 'com.github.ben-manes.versions' version '0.53.0'
  id 'com.google.osdetector' version '1.7.3'
  id 'com.diffplug.spotless' version '8.2.1'
  id 'net.nemerosa.versioning' version '3.1.0'
  id 'org.openapi.generator' version '7.19.0'
  id 'org.owasp.dependencycheck' version '12.2.0'
}

group = 'net.reini'
version = '0.0.1-SNAPSHOT'
description = "Sandbox project for pure testing purposes"
ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

java {
  sourceCompatibility = JavaVersion.VERSION_25
  withJavadocJar()
  withSourcesJar()
}

javadoc {
  exclude 'org/openapi/example/api/**'
}

repositories {
  mavenCentral()
  maven {
    url = 'https://central.sonatype.com/repository/maven-snapshots/'
  }
}

configurations {
  jdbcDrivers
  runtimeOnly.extendsFrom jdbcDrivers
}

dependencies {
  api 'org.slf4j:slf4j-api:2.0.17'

  jdbcDrivers 'com.h2database:h2:2.4.240'
  jdbcDrivers 'com.oracle.database.jdbc:ojdbc10:19.29.0.0'
  jdbcDrivers 'com.oracle.database.jdbc:ojdbc11:23.26.1.0.0'
  jdbcDrivers 'com.oracle.database.jdbc:ojdbc17:23.26.1.0.0'
  jdbcDrivers 'org.postgresql:postgresql:42.7.10'

  implementation 'com.airhacks:afterburner.fx:1.7.0'
  implementation 'com.github.sarxos:webcam-capture:0.3.12'
  implementation 'com.google.zxing:core:3.5.4'
  implementation 'com.google.zxing:javase:3.5.4'
  implementation 'javax.cache:cache-api:1.1.1'
  implementation 'org.apache.commons:commons-pool2:2.13.1'
  implementation 'org.bouncycastle:bcprov-jdk18on:1.83'
  implementation 'org.bouncycastle:bcpkix-jdk18on:1.83'
  implementation 'org.eclipse.jgit:org.eclipse.jgit:7.5.0.202512021534-r'
  implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
  implementation 'org.ehcache:ehcache:3.11.1:jakarta'
  implementation 'org.glassfish.jersey.core:jersey-client:4.0.2'
  implementation 'org.jfree:jfreechart:1.5.6'

  implementation 'jakarta.activation:jakarta.activation-api:2.1.4'
  implementation 'jakarta.jms:jakarta.jms-api:3.1.0'
  implementation 'jakarta.json.bind:jakarta.json.bind-api:3.0.1'
  implementation "jakarta.json:jakarta.json-api:2.1.3"
  implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.5'
  implementation 'org.eclipse.microprofile.config:microprofile-config-api:3.1'
  implementation 'org.eclipse.microprofile.metrics:microprofile-metrics-api:5.1.2'
  implementation 'org.eclipse.microprofile.openapi:microprofile-openapi-api:4.1.1'

  runtimeOnly 'io.smallrye.config:smallrye-config:3.16.0'
  runtimeOnly 'io.smallrye:smallrye-metrics:5.1.0'
  runtimeOnly 'org.eclipse:yasson:3.0.4'

  implementation 'org.graalvm.polyglot:polyglot:25.0.2'
  implementation 'org.graalvm.polyglot:python:25.0.2'

  implementation 'org.apache.activemq:artemis-jakarta-client:2.51.0'
  implementation 'org.apache.activemq.rest:artemis-rest:2.25.0'

  implementation 'org.jboss.weld.se:weld-se-core:6.0.4.Final'
  implementation 'org.jboss:jandex:3.3.1'

  // javaFX
  implementation "org.openjfx:javafx-base:23.0.2:$platform"
  implementation "org.openjfx:javafx-controls:23.0.2:$platform"
  implementation "org.openjfx:javafx-fxml:23.0.2:$platform"
  implementation "org.openjfx:javafx-graphics:23.0.2:$platform"

  // ssh
  implementation 'org.apache.sshd:sshd-cli:2.17.1'

  compileOnly 'jakarta.platform:jakarta.jakartaee-api:11.0.0'
  compileOnly 'jakarta.ws.rs:jakarta.ws.rs-api:4.0.0'
  compileOnly 'jakarta.websocket:jakarta.websocket-client-api:2.2.0'

  runtimeOnly 'ch.qos.logback:logback-classic:1.5.31'
  runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.6'
  runtimeOnly 'org.glassfish.jersey.core:jersey-client:4.0.2'
  runtimeOnly 'org.glassfish.jersey.media:jersey-media-json-binding:4.0.2'
  runtimeOnly 'org.glassfish.tyrus.bundles:tyrus-standalone-client:2.2.2'
  runtimeOnly 'org.glassfish.jersey.inject:jersey-hk2:4.0.2'
  runtimeOnly 'org.slf4j:jcl-over-slf4j:2.0.17'
  runtimeOnly "org.slf4j:jul-to-slf4j:2.0.17"

  testImplementation 'com.esotericsoftware:kryo:5.6.2'
  testImplementation 'com.tngtech.archunit:archunit-junit5-api:1.4.1'
  testImplementation 'easymock:easymock:2.0'
  testImplementation 'net.jqwik:jqwik:1.9.3'
  testImplementation 'org.assertj:assertj-core:3.27.7'
  testImplementation 'org.jmockit:jmockit:1.50'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.3'
  testImplementation 'org.junit.jupiter:junit-jupiter-migrationsupport:6.0.3'
  testImplementation 'org.mockito:mockito-junit-jupiter:5.21.0'
  testImplementation 'org.shredzone.acme4j:acme4j-client:4.0.0'

  testImplementation 'jakarta.websocket:jakarta.websocket-client-api:2.2.0'
  testImplementation 'org.codehaus.izpack:izpack-installer:6.0.0-SNAPSHOT'

  testRuntimeOnly 'ch.qos.logback:logback-classic:1.5.31'
  testRuntimeOnly 'com.tngtech.archunit:archunit-junit5:1.4.1'
  testRuntimeOnly 'jakarta.platform:jakarta.jakartaee-api:11.0.0'
  testRuntimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.6'
  testRuntimeOnly 'org.glassfish.jersey.core:jersey-client:4.0.2'
  testRuntimeOnly 'org.glassfish.jersey.media:jersey-media-json-binding:4.0.2'
  testRuntimeOnly 'org.glassfish.tyrus.bundles:tyrus-standalone-client:2.2.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.3'
  testRuntimeOnly 'org.junit.platform:junit-platform-jfr:1.14.3'
  testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.14.3', { exclude group: "junit", module: "junit" }
  testRuntimeOnly 'org.junit.platform:junit-platform-console:6.0.3'
  testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:6.0.3'
  testRuntimeOnly "org.slf4j:jcl-over-slf4j:2.0.17"
  testRuntimeOnly 'org.eclipse:yasson:3.0.4'
}

spotless {
  java {
    ratchetFrom('origin/master')
    licenseHeaderFile('gradle/LICENSE_HEADER').yearSeparator(', ').skipLinesMatching('.+File Name: .+')
    googleJavaFormat()
    importOrder('java', 'javax', 'jakarta', 'org', 'com')
    removeUnusedImports()
    targetExclude('**/generate-resources/main/src/gen/java/**')
  }
}

sourceSets {
  main.java.srcDirs("build/generate-resources/main/src/gen/java")
}

compileJava {
  dependsOn 'openApiGenerate'
  doFirst {
    options.compilerArgs = [
            '-Xlint:all',
            '--module-path', classpath.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml'
    ]
  }
}

tasks.named('sourcesJar') {
  dependsOn 'openApiGenerate'
}

tasks.withType(JavaCompile).each {
  it.options.compilerArgs.add('--enable-preview')
}

tasks.withType(Test).each {
  it.useJUnitPlatform()
  it.systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
  it.jvmArgs += "-XX:StartFlightRecording:dumponexit=true,filename=${layout.buildDirectory.get().asFile}/reports/${name}.jfr"
  it.jvmArgs += '--enable-preview'
  it.testLogging {
    events 'failed', 'skipped', 'passed'
  }
}

tasks.register('testSmoke', Test) {
  group 'verification'
  description 'Runs the smoke tests'
  useJUnitPlatform {
    includeTags 'smoke'
  }
}

tasks.register('testSpecial', Test) {
  group 'verification'
  description 'Runs the special tests'
  useJUnitPlatform {
    includeTags 'special'
  }
}

tasks.register('jdbcDrivers', Sync) {
  group 'custom'
  from configurations.jdbcDrivers
  into layout.buildDirectory.dir('jdbc_libs')
}

openApiGenerate {
  inputSpec = "$rootDir/swagger.yaml"
  generatorName = "jaxrs-spec"
  apiPackage = "org.openapi.example.api"
  invokerPackage = "org.openapi.example.invoker"
  modelPackage = "org.openapi.example.model"
  configOptions = [
        dateLibrary                       : 'java8',
        interfaceOnly                     : 'true',
        openApiNullable                   : 'false',
        serializableModel                 : 'true',
        useJakartaEe                      : 'true',
        useMicroProfileOpenAPIAnnotations : 'true',
        useOptional                       : 'true',
        useSwaggerAnnotations             : 'false'
  ]
}

openApiValidate {
  inputSpec = "$rootDir/swagger.yaml"
}

jar {
  manifest {
    attributes 'Implementation-Title': 'Sandbox for various test',
               'Implementation-Version': project.version,
               'Build-Revision': versioning.info.commit,
               'Automatic-Module-Name': "net.reini.sandbox"
    metaInf {
      from file('.')
      include 'LICENSE'
    }
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'pr', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
